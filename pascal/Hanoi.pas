PROGRAM HANOI (INPUT, OUTPUT);

CONST
 CASOS=3;

TYPE
 SITIOS=(ORIGEN,DESTINO,TRABAJO);
 RANGO=1..CASOS;

VAR
 TORRES:ARRAY[RANGO,SITIOS] OF INTEGER;
 I:INTEGER;
 J:SITIOS;
 DEPURA:BOOLEAN;

PROCEDURE MOVEDISCO ( LUGORG, LUGDES : SITIOS );
VAR
 BUSCAR:BOOLEAN;
 I,J:INTEGER;

FUNCTION ELEMENTO (VAR I :INTEGER; COL:SITIOS) : BOOLEAN;
VAR BUSCAR:BOOLEAN;
BEGIN
 IF DEPURA THEN WRITELN('++ENTRA ELEMENTO');
 BUSCAR:=TRUE;
 I:=1;
 WHILE (I<=CASOS) AND BUSCAR DO
  IF TORRES[I,COL]<>0 THEN BUSCAR:=FALSE
  ELSE I:=I+1;
 ELEMENTO:=BUSCAR;
 IF DEPURA THEN WRITELN('++FIN ELEMENTO,I,BUSCAR=',I,BUSCAR)
END;

BEGIN
 IF DEPURA THEN WRITELN('++ENTRA MOVEDISCO');
 BUSCAR:=ELEMENTO(I,LUGORG);
 IF BUSCAR THEN WRITELN('MOVEDISCO,ERROR LOGICO');
 BUSCAR:=ELEMENTO(J,LUGDES);
 IF BUSCAR THEN J:=CASOS ELSE J:=J-1;
 IF DEPURA THEN WRITELN('++MOVEDISCO I A J',I:4,J:4);
 TORRES[J,LUGDES]:=TORRES[I,LUGORG];
 TORRES[I,LUGORG]:=0;
 IF DEPURA THEN WRITELN('++FIN MOVEDISCO')
END;

PROCEDURE LISTDISCO;
CONST
 PALOS='      I            I            I';
 BASE1='---------------------------------------';
 BASE2='I                                      I';
 L=6;
 BLANCO=' ';
 ASTER='***********';
VAR
 I,N1,N2,N3:INTEGER;
BEGIN
 WRITELN;
 WRITELN(PALOS);
 FOR I:=1 TO CASOS DO
 BEGIN
  N1:=TORRES[I,ORIGEN];
  N2:=TORRES[I,DESTINO];
  N3:=TORRES[I,TRABAJO];
  WRITELN(
   BLANCO:(L-N1), ASTER:(N1), 'I', ASTER:(N1),BLANCO:(L-N1),
   BLANCO:(L-N2), ASTER:(N2), 'I', ASTER:(N2),BLANCO:(L-N2),
   BLANCO:(L-N3), ASTER:(N3), 'I', ASTER:(N3) )
 END;
 WRITELN(BASE1);
 WRITELN(BASE2);
 WRITELN(BASE1)
END;

PROCEDURE MOVER(N:INTEGER; N1,N2,N3:SITIOS);
BEGIN
 IF DEPURA THEN WRITELN('++ENTRA MOVER,N=',N:4);
 IF N=1 THEN MOVEDISCO(N1,N2) ELSE
 BEGIN
  MOVER(N-1,N1,N3,N2);
  LISTDISCO;
  MOVEDISCO(N1,N2);
  LISTDISCO;
  MOVER(N-1,N3,N2,N1)
 END;
 IF DEPURA THEN WRITELN('++FIN MOVER,N=',N:4)
END;

BEGIN
 WRITELN('***COMIENZA EL PROGRAMA***,CASOS=',CASOS:4);
 DEPURA:=TRUE;
 FOR I:=1 TO CASOS DO
  FOR J:=ORIGEN TO TRABAJO DO
   TORRES[I,J]:=0;
 FOR I:=1 TO CASOS DO
  TORRES[I,ORIGEN]:=I;
 WRITELN(' SITUACION DE PARTIDA');
 LISTDISCO;
 MOVER(CASOS,ORIGEN,DESTINO,TRABAJO);
 WRITELN(' SITUACION FINAL');
 LISTDISCO;
 WRITELN('***FIN DEL PROGRAMA***')
END.

