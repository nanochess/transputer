; 19-jun-1995

;
; MAnejo de Entrada y Salida de TRansputer por Oscar toledo gutierrez.
;
; (MAESTRO)
;
; Se supone que esto comienza en 8000:0100.
;
ENTRADA:
SSER: equ 64
ESER: equ 65
SBAJ: equ 66
EBAJ: equ 67
AVID: equ 68
SEMA: equ 69

bsub ; Reserva para el punto de servicio. (uso con out)
bsub
bsub
bsub
bsub ; Respuesta del punto de servicio. (uso con in)
bsub
bsub
bsub
bsub ; Reserva para el acceso a bajo nivel. (uso con out)
bsub
bsub
bsub
bsub ; Respuesta del acceso a bajo nivel. (uso con in)
bsub
bsub
bsub
bsub ; Reserva para el apuntador de video.
bsub
bsub
bsub
bsub ; Semaforo. 0 = Nadie usa el canal.
bsub ;           1 = Se esta refrescando la pantalla.
bsub ;           2 = Lo esta usando el disco o el teclado.
bsub

mint
mint
stnl SSER
mint
mint
stnl ESER
mint
mint
stnl SBAJ
mint
mint
stnl EBAJ
ajw -2000 ; Bytes para el video.
ldlp 0
mint
stnl AVID
ajw -2 ; Limpia el video.
ldlp 2
stl 0
ldc 1000
stl 1
BN1:
ldc 0x07200720
ldl 0
stnl 0
ldl 0
adc 3
stl 0
ldlp 0
ldc BN2-BN1
lend
BN2:
ldlp 1002
stl 0
ldc 1000
stl 1
BN3:
ldc 0x20072007
ldl 0
stnl 0
ldl 0
adc 3
stl 0
ldlp 0
ldc BN4-BN3
lend
BN4:
ajw 2
ldc 0
mint
stnl SEMA
ldc REFRESCO-PROC1
ldpi
PROC1:
ldlp -16
stnl -1
ldlp -16
runp
ldc ENTSAL-PROC2
ldpi
PROC2:
ldlp -32
stnl -1
ldlp -32
runp
ldc COMIENZO-PROC3
ldlp -48
startp
PROC3:
PROC4:
ldc 1000000
ldtimer
sum
tin
j PROC4

REFRESCO:
ajw -8
mint
ldnl AVID
stl 6
mint
ldnl AVID
ldnlp 1000
stl 1
R4:
ldc 50000
ldtimer
sum
tin
ldl 6
stl 2
ldl 1
stl 3
ldc 0
stl 4
R3:
ldl 4
ldc 25
diff
cj R4
ldc 0
stl 5
R1:
ldl 5
ldc 160
diff
cj R2
ldl 2
ldnl 0
ldl 3
ldnl 0
diff
eqc 0
cj REF
ldl 2
ldnlp 1
stl 2
ldl 3
ldnlp 1
stl 3
ldl 5
ldnlp 1
stl 5
j R1
R2:
ldl 4
adc 1
stl 4
j R3

REF:
mint
ldnl SEMA
cj REF1
ldc 1000
ldtimer
sum
tin
j REF
REF1:
ldc 1
mint
stnl SEMA
ldl 2
ldl 5
diff
stl 2
ldl 3
ldl 5
diff
stl 3
ldl 2
ldl 3
ldc 160
move
mint
ldnlp 0
ldc 0x30
outbyte
mint
ldnlp 0
ldc 0x34
outbyte
mint
ldnlp 0
ldl 4
outbyte
ldl 3
ldc 160
call ENVIO
ldc 0
mint
stnl SEMA
ldl 2
adc 160
stl 2
ldl 3
adc 160
stl 3
j R2

ENTSAL:
ajw -4
E1:
ldlp 0
mint
ldnlp SBAJ
ldc 4
in
E2:
mint
ldnl SEMA
cj E3
ldc 1000
ldtimer
sum
tin
j E2
E3:
ldc 2
mint
stnl SEMA
ldl 0
lb
stl 1
ldl 1
ldc 1
diff
cj LEESEC
ldl 1
ldc 2
diff
cj ESCSEC
ldl 1
ldc 3
diff
cj FORPIS
ldl 1
ldc 5
diff
cj CHETEC
ldl 1
ldc 6
diff
cj OBTHOR
ldl 1
ldc 7
diff
cj POSCUR
ldl 1
ldc 8
diff
cj FORCUR
j E6
E4:
ldl 0
ldl 1
call ENVIO
ldl 2
cj E6
ldl 0
mint
ldnlp 4
ldl 2
in
E6:
mint
ldnlp EBAJ
ldc 0
outbyte
ldc 0
mint
stnl SEMA
j E1

LEESEC:
ldc 4
stl 1
ldc 513
stl 2
j E4

ESCSEC:
ldc 516
stl 1
ldc 1
stl 2
j E4

FORPIS:
ldc 3
stl 1
ldc 1
stl 2
j E4

CHETEC:
ldc 1
stl 1
ldc 1
stl 2
j E4

OBTHOR:
ldc 1
stl 1
ldc 6
stl 2
j E4

POSCUR:
ldc 3
stl 1
ldc 0
stl 2
j E4

FORCUR:
ldc 3
stl 1
ldc 0
stl 2
j E4

ENVIO:
ajw -1
ENVIO3:
ldl 2
cj ENVIO2
mint
ldnlp 0
ldl 3
lb
ldc 4
shr
adc 0x30
outbyte
mint
ldnlp 0
ldl 3
lb
ldc 15
and
adc 0x30
outbyte
ldl 3
adc 1
stl 3
ldl 2
adc -1
stl 2
j ENVIO3
ENVIO2:
ajw 1
ret

;
; Ejecuta una secuencia de boot.
;
; Carga la pista 0, lado 0, sector 1.
;

A_VIDEO: equ 68
E_MAESTRO: equ 66
S_MAESTRO: equ 67

COMIENZO:
ajw -144
ARRANQUE2:
ldc 0 ; Arranque.
ldlp 16
call LEESECTOR
ldlp 16
stl 0
ldc FINPROG-F1
ldpi
F1:
adc 15
ldc 15
not
and
stl 1
ldlp 16
ldc 510
bsub
lb
eqc 0x12
cj F4
ldlp 16
ldc 511
bsub
lb
eqc 0x34
cj F4
ldlp 16
ldl 1
ldc 512
move
ldl 1
ajw 144
gcall

F4:
mint
ldnl A_VIDEO
ldc ERROR1-F6
ldpi
F6:
call PONE
call ETECLA
j ARRANQUE2

PONE:
ldl 1
lb
cj PONE1
ldl 1
lb
ldl 2
sb
ldl 1
adc 1
stl 1
ldl 2
adc 2
stl 2
j PONE
PONE1:
ret

ETECLA:
ajw -2
ETECLA1:
ldc 5
stl 0
ldlp 0
stl 1
ldlp 1
mint
ldnlp E_MAESTRO
ldc 4
out
ldlp 1
mint
ldnlp S_MAESTRO
ldc 1
in
ldl 0
cj ETECLA1
ajw 2
ret

LEESECTOR:
ldl 1
adc -1
stl 1
ldc 0x01
ldl 1
sb
ldl 2
ldc 36
div
ldl 1
adc 1
sb
ldl 2
ldc 18
div
ldc 2
rem
ldl 1
adc 2
sb
ldl 2
ldc 18
rem
adc 1
ldl 1
adc 3
sb
ldl 1
stl 2
ldlp 2
mint
ldnlp E_MAESTRO
ldc 4
out
ldlp 2
mint
ldnlp S_MAESTRO
ldc 1
in
ret

ERROR1:
db 'Inserte un disco '
db 'con sistema '
db 'operativo, '
db 'y pulse una '
db 'tecla...'
db 0

FINPROG:
